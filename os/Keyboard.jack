class Keyboard {

    static Array ram;
    static int keyboard;

    // init initializes Keyboard class.
    function void init() {
        let ram = 0;
        let keyboard = 24576;
        return;
    } 

    // keyPressed returns the ASCII code of the key currently pressed down. It returns 0 if no key is pressed.
    function char keyPressed() {
        return ram[keyboard];
    }

    // readChar waits for user to press any key, and displays the pressed key on the screen.
    function char readChar() {
        var char c;

        while (ram[keyboard] = 0) {
            // waits until key is pressed
        }

        let c = ram[keyboard];

        while (~(ram[keyboard] = 0)) {
            // waits until key is released
        }

        do Output.printChar(c);
        
        return c;
    }

    // readLine reads and echoes the userinput until the user presses 'enter'.
    function String readLine(String message) {
        var String str;
        var char c;

        do Output.printString(message);
        let str = String.new(50);

        while (~(c = 128)) {
            let c = Keyboard.readChar();
            if (~(c = 128)) {
                if (c = 129) {
                    do str.eraseLastChar();
                } else {
                    let str = str.appendChar(c);
                }
            }
        }

        return str;
    }   

    // readInt does similar job to readLine. It also provides char-int conversion function.
    function int readInt(String message) {
        var int n, temp;
        var char c;
        var boolean isNeg;

        do Output.printString(message);
        while (~(c = 128)) {
            let c = Keyboard.readChar();
            if (~(c = 128)) {
                if (c = 45) {
                    if (n = 0) {
                        let isNeg = true;
                    } else {
                        return n;
                    }
                } else {
                    if (c = 129) {
                        let n = n / 10;
                    } else {
                        let temp = c - 48;
                        let n = n * 10 + temp;
                    }
                }
            }
        }

        if (isNeg) {
            return -n;
        }
        return n;
    }
}
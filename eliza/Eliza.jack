class Eliza {

    function void init() {
        var int i;

        // top
        while (i < 20) {
            do Screen.drawHorizontal(10 + i, 62, 449);
            let i = i + 1;
        }
        
        // side
        let i = 0;
        while (i < 22) {
            do Screen.drawVertical(40 + i, 10, 225);
            do Screen.drawVertical(450 + i, 10, 225);
            let i = i + 1;
        }

        // corner
        let i = 0;
        while (i < 3) {
            do Screen.drawVertical(39 - i, 10 + i, 225 - i);
            do Screen.drawVertical(472 + i, 10 + i, 225 - i);
            let i = i + 1;
        }

        // bottom
        let i = 0;
        while (i < 25) {
            do Screen.drawHorizontal(201 + i, 62, 449);
            let i = i + 1;
        }

        // base
        let i = 0;
        while (i < 30) {
            do Screen.drawHorizontal(225 + i, 200 - i, 311 + i);
            let i = i + 1;
        }

        do Output.setRange(8, 55, 3, 17);

        return;
    }

    function void printIntro() {
        do Output.printString("HELLO, I'M ELIZA.");
        do Output.println();
        do Output.printString("TALK TO THE PROGRAM.");
        do Output.println();
        do Output.printString("ENTER 'QUIT' WHEN DONE.");
        do Output.println();
        return;
    }

    function void printResponse(String userInput) {
        var Array matches, keys, reflections, responses;
        var List exploded, reflected;
        var String match, out, remainder;
        var int i, j, k, numMatch, numKey, position;
        var boolean done;

        // get necessary arrays from Dictionary.jack
        let matches = Dictionary.getMatches();
        let keys = Dictionary.getKeys();
        let reflections = Dictionary.getReflections();
        let responses = Dictionary.getResponses();

        // Loop through the matches list
        let numMatch = 39;
        let numKey = 12;
        let out = responses[numMatch];
        while (i < numMatch) {
            let match = matches[i];
            let position = Strings.index(userInput, match);            
            if (position > -1) {
                let position = position + match.length() + 1;
                let exploded = Strings.split(position, userInput);
                while (j < exploded.length()) {
                    let k = 0;
                    let done = false;
                    while (k < numKey) {
                        if (Strings.isSame(exploded.extract(j), keys[k])) {
                            let reflected = reflected.append(reflections[k]);
                            let done = true;
                        }
                        let k = k + 1;
                    }
                    if (~done) {
                        let reflected = reflected.append(exploded.extract(j));
                    }
                    let j = j + 1;
                }
                let remainder = Strings.join(reflected);
                let out = responses[i];
                let i = numMatch;
            }
            let i = i + 1;
        }

        let out = Strings.replace(out, "%1", remainder);
        do Output.printString(out);

        if (position > -1) {
            do remainder.dispose();
            do out.dispose();
            do exploded.dispose();
            do reflected.dispose();
        }

        return;
    }
}